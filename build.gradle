group = "com.github.samystudio.flair"
version = "0.8.2"
description = "Flair is a gradle plugin that helps you build Adobe AIR mobile/desktop projects with Starling and Feathers libraries. Flair also comes up with its own actionscript library to manage your app resources, it actually mimics android resources managment."

buildscript {

	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5"
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
	}
}


apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "com.gradle.plugin-publish"


ext {
	bintrayRepo = "maven"
	bintrayName = project.name

	publishedGroupId = group
	libraryName = "Flair Gradle Plugin"
	artifact = project.name

	libraryDescription = description
	libraryVersion = version

	siteUrl = "https://github.com/SamYStudiO/flair-gradle-plugin"
	gitUrl = "https://github.com/SamYStudiO/flair-gradle-plugin.git"

	developerId = "samystudio"
	developerName = "Samuel Eminet"
	developerEmail = "contact@samystudio.net"

	licenseName = "BSD Simplified"
	licenseUrl = "http://opensource.org/licenses/BSD-2-Clause"
	allLicenses = [ "BSD Simplified" ]
}

sourceSets {
	main {
		resources {
			exclude "**/scaffold.iml"
		}
	}
}

repositories {
	mavenCentral( )
	mavenLocal( )
}

dependencies {
	compile gradleApi( )
	compile localGroovy( )
	testCompile gradleTestKit( )
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4" , {
		exclude group: "org.codehaus.groovy"
	}
}


task sourcesJar( type: Jar ) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar( type: Jar , dependsOn: javadoc ) {
	classifier = "javadoc"
	from javadoc.getDestinationDir( )
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

publishing {
	publications {
		flair( MavenPublication ) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
}

install {
	repositories.mavenInstaller {
		pom {
			project {
				packaging "aar"
				groupId publishedGroupId
				artifactId artifact

				// Add your description here
				name libraryName
				description libraryDescription
				url siteUrl

				// Set your license
				licenses {
					license {
						name licenseName
						url licenseUrl
					}
				}
				developers {
					developer {
						id developerId
						name developerName
						email developerEmail
					}
				}
				scm {
					connection gitUrl
					developerConnection gitUrl
					url siteUrl
				}
			}
		}
	}
}

Properties properties = new Properties( )
File f = project.rootProject.file( "local.properties" )
if( f.exists(  ) )
{
	properties.load( f.newDataInputStream( ) )

	bintray {
		user = properties.getProperty( "bintray.user" )
		key = properties.getProperty( "bintray.apikey" )

		configurations = [ "archives" ]
		pkg {
			repo = bintrayRepo
			name = bintrayName
			desc = libraryDescription
			websiteUrl = siteUrl
			vcsUrl = gitUrl
			licenses = allLicenses
			publish = true
			publicDownloadNumbers = true
		}
	}
}

pluginBundle {
	website = siteUrl
	vcsUrl = gitUrl
	description = libraryDescription
	tags = [ "adobe" , "air" , "starling" , "feathers" ]

	plugins {
		flairPlugin {
			id = publishedGroupId
			displayName = libraryName
			description = libraryDescription
		}
	}
}

